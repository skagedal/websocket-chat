import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id "application"
    id "pmd"
    id "jacoco"
    id "com.diffplug.spotless" version "6.0.0"
    id "com.github.ben-manes.versions" version "0.39.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("io.vertx:vertx-stack-depchain:4.2.1")
    implementation "io.vertx:vertx-core"
    implementation "io.vertx:vertx-web"
    implementation "io.vertx:vertx-web-client"
    implementation "io.vertx:vertx-redis-client"
    implementation "io.vertx:vertx-rx-java3"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation "org.slf4j:slf4j-api:1.7.32"
    runtimeOnly "org.slf4j:slf4j-simple:1.7.32"

    if (Os.isFamily(Os.FAMILY_MAC)) {
        runtimeOnly("io.netty:netty-resolver-dns-native-macos::osx-x86_64")
    }

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation "io.vertx:vertx-junit5:4.2.1"
    testImplementation "org.testcontainers:testcontainers:1.16.2"
    testImplementation "org.testcontainers:junit-jupiter:1.16.2"
}

application {
    mainClass = 'se.kry.chat.App'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

pmd {
    //  consoleOutput = true
    ruleSets = []
    ruleSetConfig = resources.text.fromFile("pmd.xml")
}

// Testing

test {
    systemProperty "vertx.logger-delegate-factory-class-name", "io.vertx.core.logging.SLF4JLogDelegateFactory"
//    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    testLogging.showStandardStreams = true
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = true
    }
    //noinspection GroovyAssignabilityCheck
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/generated/**')
        }))
    }
}

// Spotless disabled due to issue: https://github.com/diffplug/spotless/issues/834

spotless {
    java {
        googleJavaFormat()
    }
}

// Dependency updates

// Example check to exclude non-stable versions in recommendations from https://github.com/ben-manes/gradle-versions-plugin
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
dependencyUpdates.gradleReleaseChannel="current"